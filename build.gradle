plugins {
    id 'java-library'
    id 'maven-publish'
}

version '0.1-SNAPSHOT'
group 'de.dfki.mary'
description "Analysis toolbox for MaryTTS"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

repositories {
    jcenter()
    maven {
        url 'https://oss.jfrog.org/artifactory/repo'
    }
}

def maryttsVersion = '5.2'

dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile group: 'org.apache.commons', name:'commons-math3', version:'3.6.1'
    compile group: 'nz.ac.waikato.cms.weka', name:'weka-stable', version:'3.6.10'
    testCompile group: 'org.testng', name: 'testng', version: '6.8.13'
    compile group: 'de.dfki.mary', name: 'marytts-runtime', version: maryttsVersion
    testCompile group: 'de.dfki.mary', name: 'marytts-runtime', version: maryttsVersion

    ['en', 'de', 'fr'].each { locale ->
        testCompile group: 'de.dfki.mary', name: "marytts-lang-$locale", version: maryttsVersion
    }
    testCompile group: 'de.dfki.mary', name: 'voice-cmu-slt-hsmm', version: maryttsVersion
}


test {
    useTestNG()
    systemProperties 'property': 'value'
}


/**********************************************************************************************
 ** Formatting part
 **********************************************************************************************/

task autoFormatCode {
    def config = file("astyle.conf")
    inputs.files config
    outputs.upToDateWhen { false }
    doLast {
        fileTree(dir: '.', include: '**/*.java').each { java_file ->
            exec {
                commandLine "astyle", "--options=$config", java_file
            }
        }
    }
}

/**********************************************************************************************
 ** Project meta + publishing part
 **********************************************************************************************/

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/maven/marytts-analysis'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
